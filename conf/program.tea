/**
 * Sample RichTea program
 */
(name:"Code Snippets" version:0.1 timeStarted:0 {
	Print(name + " (Version: " + version + ")");
	
	Set(timeStarted to:GetSystemTime())

	Print("");

	Print("Increment 0 by 1 = " + (0 + 1))
	Print("Decrement 0 by 1 = " + (0 - 1))
	Print("Multiply 5 by 10 = " + (5 * 10))
	Print("Divide 10 by 2 = " + (10 / 2));

	Print("For loop: " prependNewLine:true appendNewLine:false);
	
	For(10 as:"loopCount" {
		Print(loopCount * 2 + " " appendNewLine:false);
	})
	
	Print("" prependNewLine:true)

	(from:20 to:8 {
		Print("Printing numbers from " + from + " to " + to);

		While(from >= to {
			Print(from appendNewLine:false)
			Print(", " appendNewLine:false if:from != to)
			Increment(from by:0-1)
		})
		
		Print("")
	})

	Print("");
	
	ForEach("value"  in:[false, true] {
		Print("Not " + value + " = ", appendNewLine:false);
		Print(!value ? "true" : "false");
	})

	Print("Ignore me!" if:false);
	Print("Conditional execution on any function" if:10 == 10)
	Print("")

	(x:5 {
		While(x -= 1 > 0 {
			If(x == 1 {
				Print(x + " equals one");
			} else {
				Print(x + " doesn't equal one");
			})
		})
	})

	(values:["case2", "case1", "case0"] {
	
		Print("Switching on values " + values prependNewLine:true)
	
		ForEach("elem" in:values {
			Switch(elem 
				case1 {
					Print("Switch Case 1")
				}
	
				case2 {
					Print("Switch Case 2");
				}
	
				default {
					Print("Default case called");
				}
			)
		})
	})

	Print("");

	(string:"Java Method Invocation" {
		Print(string);
		Print(" - " + string.toUpperCase + " (String.toUpperCase)")
		Print(" - " + string.toLowerCase + " (String.toLowerCase)")
	})
	
	(timeNow:GetSystemTime() {
		(timeElapsed:timeNow - timeStarted {
			Print("Program Completed in: " + timeElapsed + "ms");
		})
	})
	
	Print(RandomNumber(minimum:2 maximum:4) * RandomNumber(minimum:2 maximum:4))
	
	OpenWebpage("http://www.richwalton.co.uk/wiki")
})